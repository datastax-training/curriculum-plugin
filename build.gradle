buildscript {
  repositories {
    maven {
      url "https://plugins.gradle.org/m2/"
    }
  }
  dependencies {
    classpath "com.gradle.publish:plugin-publish-plugin:0.9.4"
  }
}

plugins {
  id 'groovy'
  id 'idea'
  id 'maven'
  id 'com.jfrog.artifactory' version '3.1.1'
  id 'jacoco'
}

apply plugin: 'com.gradle.plugin-publish'

def versionNumber = '0.3.1-SNAPSHOT'

pluginBundle {
  website = 'http://www.github.com/datastax-training'
  vcsUrl = 'http://www.github.com/datastax-training/curriculum-plugin'

  description = 'A plugin for building DataStax Asciidoctor curriculum'

  plugins {
    curriculumPlugin {
      id = 'com.datastax.gradle.curriculum.plugin'
      displayName = 'DataStax curriculum plugin'
      tags = ['curriculum', 'asciidoc', 'training', 'deckjs']
      version = versionNumber
    }
  }
}

version = versionNumber
group = 'com.datastax'
def artifact = 'gradle-curriculum-plugin'


repositories {
  jcenter()
}

dependencies {
  compile 'org.codehaus.groovy:groovy-all:2.4.7'
  compile gradleApi()

  compile 'com.github.houbie:lesscss-gradle-plugin:1.0.3-less-1.7.0'
  compile 'org.asciidoctor:asciidoctor-gradle-plugin:1.5.3'
  compile 'com.github.jruby-gradle:jruby-gradle-plugin:1.2.1'

  runtime 'org.asciidoctor:asciidoctorj-groovy-dsl:1.0.0.preview2'
  runtime 'org.asciidoctor:asciidoctorj-pdf:1.5.0-alpha.8'

  testCompile 'junit:junit:4.12'
}


jar {
  baseName = artifact
}


task wrapper(type: Wrapper) {
  gradleVersion = '3.4.1'
}


task sourceJar(type: Jar) {
  description = 'An archive of the source code for Maven Central'
  baseName = artifact
  classifier = 'sources'
  from sourceSets.main.groovy
}


task groovydocJar(type: Jar, dependsOn: groovydoc) {
  description = 'An archive of the GroovyDocs for Maven Central'
  baseName = artifact
  classifier = 'javadoc'
  from fileTree(groovydoc.destinationDir)
}
